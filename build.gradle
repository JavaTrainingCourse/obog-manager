buildscript {
    ext {
        kotlinVersion = '1.2.31'
        vaadinVersion = '8.3.3'
        checkerFrameworkVersion = '2.5.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.13'
    id 'org.springframework.boot' version '1.5.11.RELEASE'
    id "org.jetbrains.kotlin.jvm" version "1.2.31"
    id "org.jetbrains.kotlin.plugin.spring" version "1.2.31"
    id "com.github.kt3k.coveralls" version "2.8.2"
    id 'war'
    id 'groovy'
    id 'jacoco'
}

version = '0.1'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // Kotlin
    compile('org.jetbrains.kotlin:kotlin-stdlib-jre8')
    // Spring Boot
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-mail')
    // Spring Security
    compile('org.springframework.security:spring-security-web:4.2.5.RELEASE')
    // Spring Context Support
    compile('org.springframework:spring-context-support')
    // Vaadin
    compile('com.vaadin:vaadin-spring-boot-starter')
    // Utilities
    compile('org.projectlombok:lombok')
    compile('com.sun.mail:javax.mail')
    compile('org.pegdown:pegdown:1.6.0')
    compile("org.checkerframework:checker-qual:${checkerFrameworkVersion}")
    // Database
    runtime('com.h2database:h2') // development and test
    runtime('mysql:mysql-connector-java') // production
    // Tests
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.codehaus.groovy:groovy-all:2.4.10')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
    }
}

war {
    baseName = 'obog-manager'
    archiveName = baseName + "." + extension
}

springBoot {
    executable = true
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
